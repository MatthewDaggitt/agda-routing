--------------------------------------------------------------------------------
-- Agda routing library
--
-- This module contains the definition of what it means to be a dynamic
-- asynchronous iteration as well as the definition of the state function
-- and what it means for such processes to be "correct".
--------------------------------------------------------------------------------

module RoutingLib.Iteration.Asynchronous.Dynamic where

open import Level using (0â„“; _âŠ”_) renaming (suc to lsuc)
open import Level.Literals using (#_)
open import Data.Fin using (Fin)
open import Data.Fin.Properties using (all?)
open import Data.Fin.Subset using (Subset) renaming (_âˆ‰_ to _âˆ‰â‚›_)
open import Data.Fin.Subset.Properties using (_âˆˆ?_)
open import Data.Nat using (â„•; _â‰¤_; _+_; sâ‰¤s; _<_; zero; suc)
open import Data.Nat.Properties using (â‰¤-refl)
open import Data.Nat.Induction using (Acc; acc; <-wellFounded)
open import Data.Product using (âˆƒ; _Ã—_; _,_)
open import Data.Unit using (tt)
open import Function using (_âˆ˜_)
open import Relation.Binary as B using (Setoid; Rel; _Preserves_âŸ¶_; Reflexive)
open import Relation.Binary.PropositionalEquality using (_â‰¡_; _â‰¢_; refl)
open import Relation.Binary.Indexed.Homogeneous hiding (Rel)
open import Relation.Nullary using (Â¬_; yes; no)
open import Relation.Unary using (Universal; Pred; _âˆˆ_; U)
open import Relation.Unary.Properties using (U-Universal)

import RoutingLib.Relation.Binary.Indexed.Homogeneous.Construct.FiniteSubset
  as FiniteSubset
import RoutingLib.Relation.Binary.Indexed.Homogeneous.Construct.FiniteSubset.DecEquality
  as FiniteSubsetEquality
open import RoutingLib.Relation.Unary.Indexed using (IPred; _âˆˆáµ¢_; Uáµ¢; Universaláµ¢)
open import RoutingLib.Relation.Unary.Indexed.Properties using (Uáµ¢-universal)

open import RoutingLib.Iteration.Asynchronous.Dynamic.Schedule as Schedules
open import RoutingLib.Iteration.Asynchronous.Dynamic.Schedule.Pseudocycle

--------------------------------------------------------------------------------
-- Publicly re-export the notions of epochs and times so that they may
-- be imported from directly from this module.

open Schedules public
  using (Epoch; ð•‹)

--------------------------------------------------------------------------------
-- The definition of what it means for a function F to be able to be
-- iterated in an asynchronous environment.

record IsAsyncIterable
  {a n â„“}
  -- The type of data in each node
  {Sáµ¢ : Fin n â†’ Set a}
  -- The definition of equality for each node's type of data
  (_â‰ˆáµ¢_ : IRel Sáµ¢ â„“)
  -- The set of functions indexed by epoch and participants
  (F : Epoch â†’ Subset n â†’ (âˆ€ i â†’ Sáµ¢ i) â†’ (âˆ€ i â†’ Sáµ¢ i))
  -- The special state representing non-participation
  (âŠ¥ : (âˆ€ i â†’ Sáµ¢ i))
  : Set (a âŠ” â„“) where

  open FiniteSubset Sáµ¢ _â‰ˆáµ¢_ using () renaming (_âˆ¼[_]_ to _â‰ˆ[_]_) public

  -- The type of the global state of the computation
  S : Set _
  S = âˆ€ i â†’ Sáµ¢ i

  -- The definition of equality over global states
  _â‰ˆ_ : Rel S â„“
  x â‰ˆ y = âˆ€ i â†’ x i â‰ˆáµ¢ y i

  _â‰‰_ : Rel S â„“
  x â‰‰ y = Â¬ (x â‰ˆ y)

  -- Required assumptions
  field
    isDecEquivalenceáµ¢ : IsIndexedDecEquivalence Sáµ¢ _â‰ˆáµ¢_
    F-cong            : âˆ€ e p â†’ (F e p) Preserves _â‰ˆ_ âŸ¶ _â‰ˆ[ p ]_

  -- The notion of a state being in agreement with a set of participants
  Accordant : Subset n â†’ S â†’ Set _
  Accordant p x = âˆ€ {i} â†’ i âˆ‰â‚› p â†’ x i â‰ˆáµ¢ âŠ¥ i

  â‰ˆ-iDecSetoid : IndexedDecSetoid (Fin n) a â„“
  â‰ˆ-iDecSetoid = record { isDecEquivalenceáµ¢ = isDecEquivalenceáµ¢ }

  open IndexedDecSetoid â‰ˆ-iDecSetoid public
    using (_â‰Ÿáµ¢_)
    renaming
    ( reflexiveáµ¢    to â‰ˆáµ¢-reflexive
    ; reflexive     to â‰ˆ-reflexive
    ; refláµ¢         to â‰ˆáµ¢-refl
    ; refl          to â‰ˆ-refl
    ; symáµ¢          to â‰ˆáµ¢-sym
    ; sym           to â‰ˆ-sym
    ; transáµ¢        to â‰ˆáµ¢-trans
    ; trans         to â‰ˆ-trans
    ; isEquivalence to â‰ˆ-isEquivalence
    ; setoid        to â‰ˆ-setoid
    ; indexedSetoid to â‰ˆáµ¢-setoidáµ¢
    )

  _â‰Ÿ_ : B.Decidable _â‰ˆ_
  x â‰Ÿ y = all? (Î» i â†’ x i â‰Ÿáµ¢ y i)

  open FiniteSubsetEquality â‰ˆ-iDecSetoid public hiding (_â‰ˆ[_]_)

-- A bundle that contains all the required components of an
-- async iterable
record AsyncIterable a â„“ n : Set (lsuc (a âŠ” â„“)) where
  field
    Sáµ¢               : Fin n â†’ Set a
    _â‰ˆáµ¢_             : IRel Sáµ¢ â„“
    âŠ¥                : âˆ€ i â†’ Sáµ¢ i
    F                : Epoch â†’ Subset n â†’ (âˆ€ i â†’ Sáµ¢ i) â†’ (âˆ€ i â†’ Sáµ¢ i)
    isAsyncIterable  : IsAsyncIterable _â‰ˆáµ¢_ F âŠ¥

  open IsAsyncIterable isAsyncIterable public

--------------------------------------------------------------------------------
-- State function
--------------------------------------------------------------------------------
-- Given an iterable and a schedule and an initial state, returns the
-- state at time t.

module _ {a â„“ n} (I : AsyncIterable a â„“ n) (ð“¢ : Schedule n) where
  open AsyncIterable I
  open Schedule ð“¢

  -- The six cases (in-order)
  -- 1. Initially: not participating
  -- 2. Initially: participating
  -- 3. Currently: not participating
  -- 4. Currently: just started participating
  -- 5. Currently: participating but inactive
  -- 6. Currently: participating and active
  asyncIter' : S â†’ âˆ€ {t} â†’ Acc _<_ t â†’ S
  asyncIter' xâ‚€ {zero} _ i with i âˆˆ? Ï 0
  ... | no  _ = âŠ¥  i
  ... | yes _ = xâ‚€ i
  asyncIter' xâ‚€ {suc t} (acc rec) i with i âˆˆ? Ï (suc t) | i âˆˆ? Ï t | i âˆˆ? Î± (suc t)
  ... | no _  | _     | _     = âŠ¥  i
  ... | yes _ | no  _ | _     = xâ‚€ i
  ... | yes _ | yes _ | no  _ = asyncIter' xâ‚€ (rec t â‰¤-refl) i
  ... | yes _ | yes _ | yes _ = F (Î· (suc t)) (Ï (suc t))
    (Î» j â†’ asyncIter' xâ‚€ (rec (Î² (suc t) i j) (sâ‰¤s (Î²-causality t i j))) j) i

  asyncIter : S â†’ ð•‹ â†’ S
  asyncIter xâ‚€ t = asyncIter' xâ‚€ (<-wellFounded t)

--------------------------------------------------------------------------------
-- Convergent
--------------------------------------------------------------------------------
-- The notion of what it means for a dynamic asynchronous iteration to be
-- "correct".

module _ {a â„“ n} (I : AsyncIterable a â„“ n) where

  open AsyncIterable I
  
  record LocalFixedPoint (e : Epoch) (p : Subset n) : Set (a âŠ” â„“) where
    field
      x*         : S
      k*         : â„•
      x*-fixed   : F e p x* â‰ˆ x*

  -- This is a specialised definition of convergence that only
  -- guarantees the iteration is convergent when:
  --   i)  the initial state is in the set Xâ‚€,
  --   ii) the set of participants is always in the set Q.
  record PartiallyConvergent {â„“â‚} (X : IPred Sáµ¢ â„“â‚)               -- Allowable initial states
                             {â„“â‚‚} (C : Pred (Epoch Ã— Subset n) â„“â‚‚) -- Configurations in which it converges
                             : Set (# 1 âŠ” a âŠ” â„“ âŠ” â„“â‚ âŠ” â„“â‚‚) where
    field
      localFP    : âˆ€ {e p} â†’ (e , p) âˆˆ C â†’ LocalFixedPoint e p
      -- For every schedule Ïˆ , starting point xâ‚€ and point in time tâ‚›,
      -- then if the epoch and subset satisfies the predicate Q then
      -- if the schedule has k*-pseudocycles between tâ‚ and tâ‚‚
      -- then for every time tâ‚ƒ after tâ‚‚ that is within the same epoch
      -- the iteration will be at the fixed point x*.
      reachesFP : âˆ€ (Ïˆ : Schedule n) (open Schedule Ïˆ) â†’ 
                  âˆ€ {x : S} â†’ x âˆˆáµ¢ X â†’
                  âˆ€ {tâ‚› : ð•‹} (tâ‚›âˆˆC : (Î· tâ‚› , Ï tâ‚›) âˆˆ C) â†’
                  (open LocalFixedPoint (localFP tâ‚›âˆˆC)) â†’
                  âˆ€ {tâ‚˜ : ð•‹} â†’ MultiPseudocycle Ïˆ k* [ tâ‚› , tâ‚˜ ] â†’
                  âˆ€ {tâ‚‘ : ð•‹} â†’ SubEpoch Ïˆ [ tâ‚˜ , tâ‚‘ ] â†’
                  asyncIter I Ïˆ x tâ‚‘ â‰ˆ x*
   
  -- The definition below says that the iteration will always converge to
  -- a fixed point after a sufficiently long period of stability. Note
  -- that the definition does *not* guarantee that such a period of
  -- stability will ever occur. Hence why the property is named
  -- "Convergent" as opposed to "Converges".
  Convergent : Set (# 1 âŠ” a âŠ” â„“)
  Convergent = PartiallyConvergent Uáµ¢ U

--------------------------------------------------------------------------------
-- Simulation
--------------------------------------------------------------------------------
-- The notion of one asynchronous iteration simulating another. The behaviour
-- of one can therefore be reasoned about by looking at the behaviour of the
-- other.

module _ {aâ‚ aâ‚‚ â„“â‚ â„“â‚‚ n}
         (Iâˆ¥ : AsyncIterable aâ‚ â„“â‚ n)
         (Jâˆ¥ : AsyncIterable aâ‚‚ â„“â‚‚ n)
         where

  private
    module I = AsyncIterable Iâˆ¥
    module J = AsyncIterable Jâˆ¥

  record PartiallySimulates {â„“â‚ƒ} (Xâ‚€ : IPred I.Sáµ¢ â„“â‚ƒ)
                            {â„“â‚„} (Yâ‚€ : IPred J.Sáµ¢ â„“â‚„)
                            : Set (aâ‚ âŠ” aâ‚‚ âŠ” â„“â‚ âŠ” â„“â‚‚ âŠ” â„“â‚ƒ âŠ” â„“â‚„) where
    field
      toáµ¢       : âˆ€ {i} â†’ I.Sáµ¢ i â†’ J.Sáµ¢ i
      fromáµ¢     : âˆ€ {i} â†’ J.Sáµ¢ i â†’ I.Sáµ¢ i

      toáµ¢-âŠ¥     : âˆ€ {i} â†’ toáµ¢ (I.âŠ¥ i) J.â‰ˆáµ¢ J.âŠ¥ i
      toáµ¢-cong  : âˆ€ {i} {x y : I.Sáµ¢ i} â†’ x I.â‰ˆáµ¢ y â†’ toáµ¢ x J.â‰ˆáµ¢ toáµ¢ y
      toáµ¢-fromáµ¢ : âˆ€ {i} (x : J.Sáµ¢ i) â†’ toáµ¢ (fromáµ¢ x) J.â‰ˆáµ¢ x
      toáµ¢-F     : âˆ€ {e p i} x â†’ toáµ¢ (I.F e p x i) J.â‰ˆáµ¢ J.F e p (toáµ¢ âˆ˜ x) i

      from-Yâ‚€   : âˆ€ {x} â†’ x âˆˆáµ¢ Yâ‚€ â†’ (fromáµ¢ âˆ˜ x) âˆˆáµ¢ Xâ‚€
      
    to : I.S â†’ J.S
    to x i = toáµ¢ (x i)

    from : J.S â†’ I.S
    from x i = fromáµ¢ (x i)

    to-cong : âˆ€ {x y : I.S} â†’ x I.â‰ˆ y â†’ to x J.â‰ˆ to y
    to-cong xâ‰ˆy i = toáµ¢-cong (xâ‰ˆy i)

    to-F : âˆ€ {e p} â†’ (x : I.S) â†’ to (I.F e p x) J.â‰ˆ J.F e p (to x)
    to-F x i = toáµ¢-F x


  _Simulates_ : Set _
  _Simulates_ = PartiallySimulates Uáµ¢ Uáµ¢
