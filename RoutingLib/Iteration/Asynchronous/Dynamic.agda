--------------------------------------------------------------------------------
-- Dynamic asynchronous iterations
--
-- This module contains the definition of what it means to be a dynamic
-- asynchronous iteration as well as the definition of the state function
-- and what it means for such processes to be "correct".
--------------------------------------------------------------------------------

module RoutingLib.Iteration.Asynchronous.Dynamic where

open import Level using (_âŠ”_) renaming (suc to lsuc)
open import Level.Literals using (#_)
open import Data.Fin using (Fin)
open import Data.Fin.Dec using (_âˆˆ?_; all?)
open import Data.Fin.Subset using (Subset) renaming (_âˆ‰_ to _âˆ‰â‚›_)
open import Data.Nat using (â„•; _â‰¤_; _+_; sâ‰¤s; _<_; zero; suc)
open import Data.Nat.Properties using (â‰¤-refl)
open import Data.Product using (âˆƒ; _Ã—_; _,_)
open import Data.Unit using (tt)
open import Function using (_âˆ˜_)
open import Induction.WellFounded using (Acc; acc)
open import Induction.Nat using (<-wellFounded)
open import Relation.Binary as B using (Setoid; Rel; _Preserves_âŸ¶_; Reflexive)
open import Relation.Binary.PropositionalEquality using (_â‰¡_; _â‰¢_; refl)
open import Relation.Binary.Indexed.Homogeneous hiding (Rel)
open import Relation.Nullary using (Â¬_; yes; no)
open import Relation.Unary using (Universal; Pred; _âˆˆ_; U)
open import Relation.Unary.Properties using (U-Universal)

import RoutingLib.Relation.Binary.Indexed.Homogeneous.Construct.FiniteSubset
  as FiniteSubset
import RoutingLib.Relation.Binary.Indexed.Homogeneous.Construct.FiniteSubset.DecEquality
  as FiniteSubsetEquality
open import RoutingLib.Relation.Unary.Indexed using (IPred; _âˆˆáµ¢_; Uáµ¢; Universaláµ¢)
open import RoutingLib.Relation.Unary.Indexed.Properties using (Uáµ¢-universal)

open import RoutingLib.Iteration.Asynchronous.Dynamic.Schedule as Schedules
open import RoutingLib.Iteration.Asynchronous.Dynamic.Schedule.Pseudoperiod

--------------------------------------------------------------------------------
-- Publicly re-export the notions of epochs and times so that they may
-- be imported from directly from this module.

open Schedules public using (Epoch; ð•‹)

--------------------------------------------------------------------------------
-- The definition of what it means for a function F to be able to be
-- iterated in an asynchronous environment.

record IsAsyncIterable
  {a n â„“}
  -- The type of data in each node
  {Sáµ¢ : Fin n â†’ Set a}
  -- The definition of equality for each node's type of data
  (_â‰ˆáµ¢_ : IRel Sáµ¢ â„“)
  -- The set of functions indexed by epoch and participants
  (F : Epoch â†’ Subset n â†’ (âˆ€ i â†’ Sáµ¢ i) â†’ (âˆ€ i â†’ Sáµ¢ i))
  -- The special state representing non-participation
  (âŠ¥ : (âˆ€ i â†’ Sáµ¢ i))
  : Set (a âŠ” â„“) where

  open FiniteSubset Sáµ¢ _â‰ˆáµ¢_ using () renaming (_âˆ¼[_]_ to _â‰ˆ[_]_) public

  -- The type of the global state of the computation
  S : Set _
  S = âˆ€ i â†’ Sáµ¢ i

  -- The definition of equality over global states
  _â‰ˆ_ : Rel S â„“
  x â‰ˆ y = âˆ€ i â†’ x i â‰ˆáµ¢ y i

  _â‰‰_ : Rel S â„“
  x â‰‰ y = Â¬ (x â‰ˆ y)

  -- Required assumptions
  field
    isDecEquivalenceáµ¢ : IsIndexedDecEquivalence Sáµ¢ _â‰ˆáµ¢_
    F-cong            : âˆ€ e p â†’ (F e p) Preserves _â‰ˆ_ âŸ¶ _â‰ˆ[ p ]_

  -- The notion of a state being in agreement with a set of participants
  Accordant : Subset n â†’ S â†’ Set _
  Accordant p x = âˆ€ {i} â†’ i âˆ‰â‚› p â†’ x i â‰ˆáµ¢ âŠ¥ i

  â‰ˆ-iDecSetoid : IndexedDecSetoid (Fin n) a â„“
  â‰ˆ-iDecSetoid = record { isDecEquivalenceáµ¢ = isDecEquivalenceáµ¢ }

  open IndexedDecSetoid â‰ˆ-iDecSetoid public
    using (_â‰Ÿáµ¢_)
    renaming
    ( reflexiveáµ¢    to â‰ˆáµ¢-reflexive
    ; reflexive     to â‰ˆ-reflexive
    ; refláµ¢         to â‰ˆáµ¢-refl
    ; refl          to â‰ˆ-refl
    ; symáµ¢          to â‰ˆáµ¢-sym
    ; sym           to â‰ˆ-sym
    ; transáµ¢        to â‰ˆáµ¢-trans
    ; trans         to â‰ˆ-trans
    ; isEquivalence to â‰ˆ-isEquivalence
    ; setoid        to â‰ˆ-setoid
    ; indexedSetoid to â‰ˆáµ¢-setoidáµ¢
    )

  _â‰Ÿ_ : B.Decidable _â‰ˆ_
  x â‰Ÿ y = all? (Î» i â†’ x i â‰Ÿáµ¢ y i)

  open FiniteSubsetEquality â‰ˆ-iDecSetoid public hiding (_â‰ˆ[_]_)

-- A package that contains all the required components of an
-- async iterable
record AsyncIterable a â„“ n : Set (lsuc (a âŠ” â„“)) where
  field
    Sáµ¢               : Fin n â†’ Set a
    _â‰ˆáµ¢_             : IRel Sáµ¢ â„“
    âŠ¥                : âˆ€ i â†’ Sáµ¢ i
    F                : Epoch â†’ Subset n â†’ (âˆ€ i â†’ Sáµ¢ i) â†’ (âˆ€ i â†’ Sáµ¢ i)
    isAsyncIterable  : IsAsyncIterable _â‰ˆáµ¢_ F âŠ¥

  open IsAsyncIterable isAsyncIterable public

--------------------------------------------------------------------------------
-- State function
--------------------------------------------------------------------------------
-- Given an iterable and a schedule and an initial state, returns the
-- state at time t.

module _ {a â„“ n} (I : AsyncIterable a â„“ n) (ð“¢ : Schedule n) where
  open AsyncIterable I
  open Schedule ð“¢

  -- The six cases (in-order)
  -- 1. Initially: not participating
  -- 2. Initially: participating
  -- 3. Currently: not participating
  -- 4. Currently: just started participating
  -- 5. Currently: participating but inactive
  -- 6. Currently: participating and active
  asyncIter' : S â†’ âˆ€ {t} â†’ Acc _<_ t â†’ S
  asyncIter' xâ‚€ {zero} _ i with i âˆˆ? Ï 0
  ... | no  _ = âŠ¥  i
  ... | yes _ = xâ‚€ i
  asyncIter' xâ‚€ {suc t} (acc rec) i with i âˆˆ? Ï (suc t) | i âˆˆ? Ï t | i âˆˆ? Î± (suc t)
  ... | no _  | _     | _     = âŠ¥  i
  ... | yes _ | no  _ | _     = xâ‚€ i
  ... | yes _ | yes _ | no  _ = asyncIter' xâ‚€ (rec t â‰¤-refl) i
  ... | yes _ | yes _ | yes _ = F (Î· (suc t)) (Ï (suc t))
    (Î» j â†’ asyncIter' xâ‚€ (rec (Î² (suc t) i j) (sâ‰¤s (Î²-causality t i j))) j) i

  asyncIter : S â†’ ð•‹ â†’ S
  asyncIter xâ‚€ t = asyncIter' xâ‚€ (<-wellFounded t)

--------------------------------------------------------------------------------
-- Convergent
--------------------------------------------------------------------------------
-- The notion of what it means for a dynamic asynchronous iteration to be
-- "correct".

module _ {a â„“ n} (I : AsyncIterable a â„“ n) where

  open AsyncIterable I
  open Schedule

  -- The definition below says that the iteration will always converge to
  -- a fixed point after a sufficiently long period of stability. Note
  -- that the definition does *not* guarantee that such a period of
  -- stability will ever occur. Hence why the property is named
  -- "Convergent" as opposed to "Converges".
  record Convergent : Set (# 1 âŠ” a âŠ” â„“) where
    field
      -- The fixed point reached for each epoch and set of participants
      x*         : Epoch â†’ Subset n â†’ S
      -- The number of pseudo-cycles required to reach said fixed point
      k*         : Epoch â†’ Subset n â†’ â„•
      -- The fixed points are truly fixed points
      x*-fixed   : âˆ€ e p â†’ F e p (x* e p) â‰ˆ x* e p
      -- For every schedule S , starting point xâ‚€ and point in time tâ‚›,
      -- then if the schedule is k*-pseudoperiodic between tâ‚ and tâ‚‚
      -- then for every time tâ‚ƒ after tâ‚‚ that is within the same epoch
      -- the iteration will be at the fixed point x*.
      x*-reached : âˆ€ (xâ‚€ : S) (S : Schedule n) â†’
                   âˆ€ {tâ‚› tâ‚˜ : ð•‹} â†’
                   MultiPseudocycle S (k* (Î· S tâ‚›) (Ï S tâ‚›)) [ tâ‚› , tâ‚˜ ] â†’
                   âˆ€ {tâ‚‘ : ð•‹} â†’ SubEpoch S [ tâ‚˜ , tâ‚‘ ] â†’
                   asyncIter I S xâ‚€ tâ‚‘ â‰ˆ x* (Î· S tâ‚›) (Ï S tâ‚›)

  -- This is a specialised definition that only guarantees the iteration
  -- is convergent when the initial state is in the set Xâ‚€ and the set of
  -- participants is always in the set Q.
  record PartiallyConvergent {â„“â‚ â„“â‚‚} (Xâ‚€ : IPred Sáµ¢ â„“â‚) (Q : Pred (Subset n) â„“â‚‚)
                             : Set (# 1 âŠ” a âŠ” â„“ âŠ” â„“â‚ âŠ” â„“â‚‚) where
    field
      x*         : Epoch â†’ âˆ€ {p} â†’ p âˆˆ Q â†’ S
      k*         : Epoch â†’ âˆ€ {p} â†’ p âˆˆ Q â†’ â„•
      x*-fixed   : âˆ€ e {p} (pâˆˆQ : p âˆˆ Q) â†’ F e p (x* e pâˆˆQ) â‰ˆ x* e pâˆˆQ
      x*-reached : âˆ€ {xâ‚€} â†’ xâ‚€ âˆˆáµ¢ Xâ‚€ â†’
                   âˆ€ {S : Schedule n} (ÏâˆˆQ : S satisfies Q) â†’
                   âˆ€ {tâ‚› tâ‚˜ : ð•‹} â†’
                   MultiPseudocycle S (k* (Î· S tâ‚›) (ÏâˆˆQ tâ‚›)) [ tâ‚› , tâ‚˜ ] â†’
                   âˆ€ {tâ‚‘} â†’ SubEpoch S [ tâ‚˜ , tâ‚‘ ] â†’
                   asyncIter I S xâ‚€ tâ‚‘ â‰ˆ x* (Î· S tâ‚›) (ÏâˆˆQ tâ‚›)

-- The relationship between convergent and partially convergent

module _ {a â„“ n} {I : AsyncIterable a â„“ n} where

  open AsyncIterable I

  convergentâ‡’partiallyConvergent : Convergent I â†’ PartiallyConvergent I Uáµ¢ U
  convergentâ‡’partiallyConvergent convergent = record
    { x*         = Î» e {p} _ â†’ x* e p
    ; k*         = Î» e {p} _ â†’ k* e p
    ; x*-fixed   = Î» e {p} _ â†’ x*-fixed e p
    ; x*-reached = Î» {xâ‚€} _ {S} _ â†’ x*-reached xâ‚€ S
    } where open Convergent convergent

  partiallyConvergentâ‡’convergent : âˆ€ {â„“â‚} {Xâ‚€ : IPred Sáµ¢ â„“â‚} â†’ Universaláµ¢ Xâ‚€ â†’
                                   âˆ€ {â„“â‚‚} {Q : Pred (Subset n) â„“â‚‚} â†’ Universal Q â†’
                                   PartiallyConvergent I Xâ‚€ Q â†’ Convergent I
  partiallyConvergentâ‡’convergent (_âˆˆXâ‚€) (_âˆˆQ) pConvergent = record
    { x*         = Î» e p â†’ x* e (p âˆˆQ)
    ; k*         = Î» e p â†’ k* e (p âˆˆQ)
    ; x*-fixed   = Î» e p â†’ x*-fixed e (p âˆˆQ)
    ; x*-reached = Î» xâ‚€ S â†’ x*-reached (Î» i â†’ _ âˆˆXâ‚€) (Î» t â†’ _ âˆˆQ)
    } where open PartiallyConvergent pConvergent

  partiallyConvergentâ‡’convergentâ€² : PartiallyConvergent I Uáµ¢ U â†’ Convergent I
  partiallyConvergentâ‡’convergentâ€² = partiallyConvergentâ‡’convergent
    (Uáµ¢-universal Sáµ¢) U-Universal

--------------------------------------------------------------------------------
-- Simulation
--------------------------------------------------------------------------------
-- The notion of one asynchronous iteration simulating another. The behaviour
-- of one can therefore be reasoned about by looking at the behaviour of the
-- other.

module _ {aâ‚ aâ‚‚ â„“â‚ â„“â‚‚ n}
         (Iâˆ¥ : AsyncIterable aâ‚ â„“â‚ n)
         (Jâˆ¥ : AsyncIterable aâ‚‚ â„“â‚‚ n)
         where

  private
    module P = AsyncIterable Iâˆ¥
    module Q = AsyncIterable Jâˆ¥

  record Simulates : Set (aâ‚ âŠ” aâ‚‚ âŠ” â„“â‚ âŠ” â„“â‚‚) where
    field
      toáµ¢       : âˆ€ {i} â†’ P.Sáµ¢ i â†’ Q.Sáµ¢ i
      fromáµ¢     : âˆ€ {i} â†’ Q.Sáµ¢ i â†’ P.Sáµ¢ i

      toáµ¢-âŠ¥     : âˆ€ {i} â†’ toáµ¢ (P.âŠ¥ i) Q.â‰ˆáµ¢ Q.âŠ¥ i
      toáµ¢-cong  : âˆ€ {i} {x y : P.Sáµ¢ i} â†’ x P.â‰ˆáµ¢ y â†’ toáµ¢ x Q.â‰ˆáµ¢ toáµ¢ y
      toáµ¢-fromáµ¢ : âˆ€ {i} (x : Q.Sáµ¢ i) â†’ toáµ¢ (fromáµ¢ x) Q.â‰ˆáµ¢ x
      toáµ¢-F     : âˆ€ {i e p} x â†’ toáµ¢ (P.F e p x i) Q.â‰ˆáµ¢ Q.F e p (toáµ¢ âˆ˜ x) i

    to : P.S â†’ Q.S
    to x i = toáµ¢ (x i)

    from : Q.S â†’ P.S
    from x i = fromáµ¢ (x i)

    to-cong : âˆ€ {x y : P.S} â†’ x P.â‰ˆ y â†’ to x Q.â‰ˆ to y
    to-cong xâ‰ˆy i = toáµ¢-cong (xâ‰ˆy i)

    to-F : âˆ€ {e p} (x : P.S) â†’ to (P.F e p x) Q.â‰ˆ Q.F e p (to x)
    to-F x i = toáµ¢-F x
