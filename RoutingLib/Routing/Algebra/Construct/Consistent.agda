--------------------------------------------------------------------------------
-- This module defines the notion of a value of a route being consistent with
-- the current network. This means that if you traversed the path along which
-- it claims it was generated along you would arrive at the same value. For
-- example a route may be inconsistent with the current network topology if a
-- link on it's path has since failed or its weight has changed.
--------------------------------------------------------------------------------

open import RoutingLib.Routing using (AdjacencyMatrix)
open import RoutingLib.Routing.Algebra

module RoutingLib.Routing.Algebra.Construct.Consistent
  {a b â„“ n} {algebra : RawRoutingAlgebra a b â„“}
  (isRoutingAlgebra : IsRoutingAlgebra algebra)
  (isPathAlgebra : IsCertifiedPathAlgebra algebra n)
  (A : AdjacencyMatrix algebra n)
  where

open import Algebra.FunctionProperties as AlgebraicProperties using (Opâ‚‚)
open import Data.Fin as Fin using (Fin)
open import Data.List using (List; map)
import Data.List.Membership.Setoid as Membership
open import Data.List.Membership.Setoid.Properties using (âˆˆ-resp-â‰ˆ; âˆˆ-mapâº)
open import Data.Maybe using (Maybe; nothing; just)
open import Data.Nat using (suc)
open import Data.Product using (Î£; _Ã—_; _,_; projâ‚)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Function using (_on_)
open import Level using (_âŠ”_) renaming (zero to 0â„“)
open import Relation.Binary as B hiding (Decidable)
open import Relation.Binary.PropositionalEquality using (inspect; [_]; _â‰¡_; _â‰¢_; refl; sym; trans)
import Relation.Binary.Construct.On as On
import Relation.Binary.EqReasoning as EqReasoning
open import Relation.Unary as U hiding (Decidable; U)
open import Relation.Nullary using (Â¬_; yes; no)
open import Relation.Nullary.Negation using (contradiction)

open import RoutingLib.Relation.Nullary.Decidable using (toSum)

open import RoutingLib.Data.Path.CertifiedI
open import RoutingLib.Data.Path.CertifiedI.Enumeration
open import RoutingLib.Data.Path.CertifiedI.Properties
import RoutingLib.Routing.Algebra.Properties.CertifiedPathAlgebra
  as PathAlgebraProperties
import RoutingLib.Routing.Algebra.Properties.RoutingAlgebra
  as RoutingAlgebraProperties

open RawRoutingAlgebra algebra
open IsRoutingAlgebra isRoutingAlgebra
open IsCertifiedPathAlgebra isPathAlgebra
open PathAlgebraProperties isRoutingAlgebra isPathAlgebra
open RoutingAlgebraProperties isRoutingAlgebra

--------------------------------------------------------------------------------
-- Definition
--------------------------------------------------------------------------------
-- A route is consistent if it is equal to the weight of the path along which
-- it was generated.

ğ‘ª : Route â†’ Set â„“
ğ‘ª r = weight A (path r) â‰ˆ r

ğ‘° : Route â†’ Set â„“
ğ‘° r = Â¬ ğ‘ª r

--------------------------------------------------------------------------------
-- Some simple properties

ğ‘ª? : U.Decidable ğ‘ª
ğ‘ª? r = weight A (path r) â‰Ÿ r

ğ‘ª-cong : âˆ€ {r s} â†’ r â‰ˆ s â†’ ğ‘ª r â†’ ğ‘ª s
ğ‘ª-cong râ‰ˆs rá¶œ = â‰ˆ-trans (â‰ˆ-trans (weight-cong (path-cong (â‰ˆ-sym râ‰ˆs))) rá¶œ) râ‰ˆs

ğ‘°-cong : âˆ€ {r s} â†’ r â‰ˆ s â†’ ğ‘° r â†’ ğ‘° s
ğ‘°-cong râ‰ˆs râ± sá¶œ = râ± (ğ‘ª-cong (â‰ˆ-sym râ‰ˆs) sá¶œ)

ğ‘ªğ‘°â‡’â‰‰ : âˆ€ {r s} â†’ ğ‘ª r â†’ ğ‘° s â†’ r â‰‰ s
ğ‘ªğ‘°â‡’â‰‰ rá¶œ sâ± râ‰ˆs = sâ± (ğ‘ª-cong râ‰ˆs rá¶œ)

0á¶œ : ğ‘ª 0#
0á¶œ = weight-cong p[0]â‰ˆ[]

âˆá¶œ : ğ‘ª âˆ
âˆá¶œ = weight-cong p[âˆ]â‰ˆâˆ…

âŠ•-pres-ğ‘ª : âˆ€ {r s} â†’ ğ‘ª r â†’ ğ‘ª s â†’ ğ‘ª (r âŠ• s)
âŠ•-pres-ğ‘ª {r} {s} rá¶œ sá¶œ with âŠ•-sel r s
... | injâ‚ râŠ•sâ‰ˆr = ğ‘ª-cong (â‰ˆ-sym râŠ•sâ‰ˆr) rá¶œ
... | injâ‚‚ râŠ•sâ‰ˆs = ğ‘ª-cong (â‰ˆ-sym râŠ•sâ‰ˆs) sá¶œ

â–·-pres-ğ‘ª : âˆ€ i j {r} â†’ ğ‘ª r â†’ ğ‘ª (A i j â–· r)
â–·-pres-ğ‘ª i j {r} rá¶œ with A i j â–· r â‰Ÿ âˆ
... | yes Aáµ¢â±¼â–·râ‰ˆâˆ = ğ‘ª-cong (â‰ˆ-sym Aáµ¢â±¼â–·râ‰ˆâˆ) âˆá¶œ
... | no  Aáµ¢â±¼â–·râ‰‰âˆ with path r | inspect path r
...   | invalid | [ páµ£â‰¡âˆ… ] = contradiction (p[r]â‰¡âˆ…â‡’fâ–·râ‰ˆâˆ (A i j) páµ£â‰¡âˆ…) Aáµ¢â±¼â–·râ‰‰âˆ
...   | valid q | [ páµ£â‰¡q ] with â‰ˆâ‚š-reflexive páµ£â‰¡q | (i , j) â‡¿áµ¥? q | i âˆ‰áµ¥â‚š? q
...     | páµ£â‰ˆq | no Â¬ijâ‡¿q | _       = ğ‘ª-cong (â‰ˆ-sym (path-reject (A i j) páµ£â‰ˆq (injâ‚ Â¬ijâ‡¿q))) âˆá¶œ -- páµ£â‰ˆq
...     | páµ£â‰ˆq | _        | no  iâˆˆq = ğ‘ª-cong (â‰ˆ-sym (path-reject (A i j) páµ£â‰ˆq (injâ‚‚ iâˆˆq))) âˆá¶œ -- páµ£â‰ˆq
...     | páµ£â‰ˆq | yes ijâ‡¿q | yes iâˆ‰q = begin
  weight A (path (A i j â–· r))                   â‰ˆâŸ¨ weight-cong {_} {path (A i j â–· r)} (path-accept (A i j) páµ£â‰ˆq Aáµ¢â±¼â–·râ‰‰âˆ ijâ‡¿q iâˆ‰q) âŸ©
  weight A (valid ((i , j) âˆ· q âˆ£ ijâ‡¿q âˆ£ iâˆ‰q))   â‰¡âŸ¨âŸ©
  A i j â–· weight A (valid q)                    â‰ˆâŸ¨ â–·-cong (A i j) rá¶œ âŸ©
  A i j â–· r                                     âˆ
  where open EqReasoning S

â–·-forces-ğ‘° : âˆ€ {i j r} â†’ ğ‘° (A i j â–· r) â†’ ğ‘° r
â–·-forces-ğ‘° fâ–·râ± rá¶œ = fâ–·râ± (â–·-pres-ğ‘ª _ _ rá¶œ)

weightá¶œ : âˆ€ p â†’ ğ‘ª (weight A p)
weightá¶œ invalid                            = âˆá¶œ
weightá¶œ (valid [])                         = 0á¶œ
weightá¶œ (valid ((i , j) âˆ· p âˆ£ eâ‡¿p âˆ£ eâˆ‰p)) with A i j â–· weight A (valid p) â‰Ÿ âˆ
... | yes Aáµ¢â±¼â–·wâ‚šâ‰ˆâˆ = ğ‘ª-cong (â‰ˆ-sym Aáµ¢â±¼â–·wâ‚šâ‰ˆâˆ) âˆá¶œ
... | no  Aáµ¢â±¼â–·wâ‚šâ‰‰âˆ with path (weight A (valid p)) | inspect path (weight A (valid p))
...   | invalid | [ p[wâ‚š]â‰¡âˆ… ] = ğ‘ª-cong (â‰ˆ-sym (p[r]â‰¡âˆ…â‡’fâ–·râ‰ˆâˆ (A i j) p[wâ‚š]â‰¡âˆ…)) âˆá¶œ
...   | valid q | [ p[wâ‚š]â‰¡q ] with â‰ˆâ‚š-reflexive p[wâ‚š]â‰¡q | (i , j) â‡¿áµ¥? q | i âˆ‰áµ¥â‚š? q
...     | p[wâ‚š]â‰ˆq | no Â¬ijâ‡¿q | _       = ğ‘ª-cong (â‰ˆ-sym (path-reject (A i j) p[wâ‚š]â‰ˆq (injâ‚ Â¬ijâ‡¿q))) âˆá¶œ
...     | p[wâ‚š]â‰ˆq | _        | no  iâˆˆq = ğ‘ª-cong (â‰ˆ-sym (path-reject (A i j) p[wâ‚š]â‰ˆq (injâ‚‚ iâˆˆq))) âˆá¶œ
...     | p[wâ‚š]â‰ˆq | yes ijâ‡¿q | yes iâˆ‰q = begin
  weight A (path (A i j â–· weight A (valid p)))  â‰ˆâŸ¨ weight-cong (path-accept (A i j) p[wâ‚š]â‰ˆq Aáµ¢â±¼â–·wâ‚šâ‰‰âˆ ijâ‡¿q iâˆ‰q) âŸ©
  weight A (valid ((i , j) âˆ· q âˆ£ ijâ‡¿q âˆ£ iâˆ‰q))   â‰¡âŸ¨âŸ©
  A i j â–· weight A (valid q)                    â‰ˆâŸ¨ â–·-cong (A i j) (weight-cong (â‰ˆâ‚š-sym p[wâ‚š]â‰ˆq)) âŸ©
  A i j â–· weight A (path (weight A (valid p)))  â‰ˆâŸ¨ â–·-cong (A i j) (weightá¶œ (valid p)) âŸ©
  A i j â–· weight A (valid p)                    âˆ
  where open EqReasoning S

sizeâ±-incr : âˆ€ {i j : Fin n} {r} {f : Step i j} â†’ ğ‘° (f â–· r) â†’ suc (size r) â‰¡ size (f â–· r)
sizeâ±-incr {i} {j} {r} {f} fâ–·râ± with f â–· r â‰Ÿ âˆ
... | yes fâ–·râ‰ˆâˆ = contradiction (ğ‘ª-cong (â‰ˆ-sym fâ–·râ‰ˆâˆ) âˆá¶œ) fâ–·râ±
... | no  fâ–·râ‰‰âˆ with path r | inspect path r
...   | invalid | [ páµ£â‰¡âˆ… ] = contradiction (p[r]â‰¡âˆ…â‡’fâ–·râ‰ˆâˆ f páµ£â‰¡âˆ…) fâ–·râ‰‰âˆ
...   | valid q | [ páµ£â‰¡q ] with â‰ˆâ‚š-reflexive páµ£â‰¡q | (i , j) â‡¿áµ¥? q | i âˆ‰áµ¥â‚š? q
...     | páµ£â‰ˆq | no Â¬ijâ‡¿q | _       = contradiction (path-reject f páµ£â‰ˆq (injâ‚ Â¬ijâ‡¿q)) fâ–·râ‰‰âˆ
...     | páµ£â‰ˆq | _        | no  iâˆˆq = contradiction (path-reject f páµ£â‰ˆq (injâ‚‚ iâˆˆq)) fâ–·râ‰‰âˆ
...     | páµ£â‰ˆq | yes ijâ‡¿q | yes iâˆ‰q = sym (length-cong (path-accept f páµ£â‰ˆq fâ–·râ‰‰âˆ ijâ‡¿q iâˆ‰q))

sizeâ±-incrâ€² : âˆ€ {i j : Fin n} {r s} {f : Step i j} â†’ ğ‘° s â†’ s â‰ˆ f â–· r â†’ suc (size r) â‰¡ size s
sizeâ±-incrâ€² sâ± sâ‰ˆfâ–·r = trans (sizeâ±-incr (ğ‘°-cong sâ‰ˆfâ–·r sâ±)) (size-cong (â‰ˆ-sym sâ‰ˆfâ–·r))

--------------------------------------------------------------------------------
-- The consistent routing algebra
--------------------------------------------------------------------------------
-- The subset of routes consistent with the current network topology form a
-- finite routing algebra

-- A consistent route is simply a route paired with a proof that it is
-- consistent.

CRoute : Set _
CRoute = Î£ Route ğ‘ª

toCRoute : âˆ€ {r} â†’ ğ‘ª r â†’ CRoute
toCRoute {r} rá¶œ = r , rá¶œ

fromCRoute : CRoute â†’ Route
fromCRoute (r , _ ) = r

-- The sets of edge functions for the consistent routing algebra are a little
-- harder to define. The edges are labelled with the arcs, that are then
-- used to index into the current network topology. The problem is that they
-- technically they need to work for all sizes of networks. Therefore the
-- arc indexes (i.e. i and j from CStep i j) are discarded, and only the
-- contents of the arc (Fin n Ã— Fin n) are used. The type has to be extended
-- to Maybe (Fin n Ã— Fin n) to ensure that the algebra has an invalid edge fâˆ.
-- Finally to ensure that i and j are still inferable by Agda, it is is
-- necessary to append the no-op type (i â‰¡ j âŠ i â‰¢ j). Basically it's all
-- an ugly hack but it seems to work.

CStep : âˆ€ {m} â†’ Fin m â†’ Fin m â†’ Set
CStep i j = Maybe (Fin n Ã— Fin n) Ã— (i â‰¡ j âŠ i â‰¢ j)

-- The trivial route is simply taken from the original algebra

C0# : CRoute
C0# = 0# , 0á¶œ

-- The invalid route is simply taken from the original algebra

Câˆ : CRoute
Câˆ = âˆ , âˆá¶œ

-- Equality over consistent routes is equality on the route

infix 4 _â‰ˆá¶œ_ _â‰‰á¶œ_ _â‰Ÿá¶œ_

_â‰ˆá¶œ_ : Rel CRoute _
_â‰ˆá¶œ_ = _â‰ˆ_ on projâ‚

_â‰‰á¶œ_ : Rel CRoute _
r â‰‰á¶œ s = Â¬ (r â‰ˆá¶œ s)

-- Again the choice operator is simply lifted to consistent routes

infix 7 _âŠ•á¶œ_

_âŠ•á¶œ_ : Opâ‚‚ CRoute
(r , rá¶œ) âŠ•á¶œ (s , sá¶œ) = r âŠ• s , âŠ•-pres-ğ‘ª rá¶œ sá¶œ

-- Extension works a little differently. The arc containing `nothing` is the
-- invalid arc. For the arc (k , l), extending the route is performed by
-- applying the original edge weight A k l in the network topology.

infix 6 _â–·á¶œ_

_â–·á¶œ_ : âˆ€{n} {i j : Fin n} â†’ CStep i j â†’ CRoute â†’ CRoute
(nothing       , _) â–·á¶œ (r , rá¶œ) = Câˆ
(valid (k , l) , _) â–·á¶œ (r , rá¶œ) = A k l â–· r , â–·-pres-ğ‘ª k l rá¶œ
-- As mentioned the invalid arc weight is simply `nothing`

fâˆá¶œ : âˆ€ {n} (i j : Fin n) â†’ CStep i j
fâˆá¶œ i j = nothing , toSum (i Fin.â‰Ÿ j)

-- As expected, equality obeys all the required properties

open AlgebraicProperties _â‰ˆá¶œ_

_â‰Ÿá¶œ_ : B.Decidable _â‰ˆá¶œ_
_ â‰Ÿá¶œ _ = _ â‰Ÿ _

â‰ˆá¶œ-isDecEquivalence : IsDecEquivalence _â‰ˆá¶œ_
â‰ˆá¶œ-isDecEquivalence = On.isDecEquivalence projâ‚ â‰ˆ-isDecEquivalence

Sá¶œ : Setoid _ _
Sá¶œ = On.setoid {B = CRoute} S projâ‚

DSá¶œ : DecSetoid _ _
DSá¶œ = On.decSetoid {B = CRoute} DS projâ‚

âŠ•á¶œ-cong : Congruentâ‚‚ _âŠ•á¶œ_
âŠ•á¶œ-cong = âŠ•-cong

â–·á¶œ-cong : âˆ€ {n} {i j : Fin n} (f : CStep i j) {r s} â†’ r â‰ˆá¶œ s â†’ f â–·á¶œ r â‰ˆá¶œ f â–·á¶œ s
â–·á¶œ-cong (nothing       , _) = Î» _ â†’ â‰ˆ-refl
â–·á¶œ-cong (valid (k , l) , _) = â–·-cong (A k l)

fâˆá¶œ-reject : âˆ€ {n} (i j : Fin n) â†’ âˆ€ x â†’ (fâˆá¶œ i j) â–·á¶œ x â‰ˆá¶œ Câˆ
fâˆá¶œ-reject _ _ _ = â‰ˆ-refl

-- Finally the raw routing algebra may be formed

algebraá¶œ : RawRoutingAlgebra (a âŠ” â„“) 0â„“ â„“
algebraá¶œ = record
  { Step               = CStep
  ; Route              = CRoute
  ; _â‰ˆ_                = _â‰ˆá¶œ_
  ; _âŠ•_                = _âŠ•á¶œ_
  ; _â–·_                = _â–·á¶œ_
  ; 0#                 = C0#
  ; âˆ                  = Câˆ
  ; fâˆ                 = fâˆá¶œ
  ; â‰ˆ-isDecEquivalence = â‰ˆá¶œ-isDecEquivalence
  ; âŠ•-cong             = âŠ•-cong
  ; â–·-cong             = â–·á¶œ-cong
  ; fâˆ-reject          = fâˆá¶œ-reject
  }

------------------------------------------------------------------------------
-- The consistent algebra obeys all the properties of a routing algebra

âŠ•á¶œ-assoc : Associative _âŠ•á¶œ_
âŠ•á¶œ-assoc _ _ _ = âŠ•-assoc _ _ _

âŠ•á¶œ-comm : Commutative _âŠ•á¶œ_
âŠ•á¶œ-comm _ _ = âŠ•-comm _ _

âŠ•á¶œ-sel : Selective _âŠ•á¶œ_
âŠ•á¶œ-sel _ _ = âŠ•-sel _ _

âŠ•á¶œ-zeroÊ³ : RightZero C0# _âŠ•á¶œ_
âŠ•á¶œ-zeroÊ³ _ = âŠ•-zeroÊ³ _

âŠ•á¶œ-identityÊ³ : RightIdentity Câˆ _âŠ•á¶œ_
âŠ•á¶œ-identityÊ³ _ = âŠ•-identityÊ³ _

â–·á¶œ-fixedPoint : âˆ€ {n} {i j : Fin n} (f : CStep i j) â†’ f â–·á¶œ Câˆ â‰ˆá¶œ Câˆ
â–·á¶œ-fixedPoint (nothing       , _) = â‰ˆ-refl
â–·á¶œ-fixedPoint (valid (k , l) , _) = â–·-fixedPoint (A k l)

isRoutingAlgebraá¶œ : IsRoutingAlgebra algebraá¶œ
isRoutingAlgebraá¶œ = record
  { âŠ•-sel        = âŠ•á¶œ-sel
  ; âŠ•-comm       = âŠ•á¶œ-comm
  ; âŠ•-assoc      = âŠ•á¶œ-assoc
  ; âŠ•-zeroÊ³      = âŠ•á¶œ-zeroÊ³
  ; âŠ•-identityÊ³  = âŠ•á¶œ-identityÊ³
  ; â–·-fixedPoint = â–·á¶œ-fixedPoint
  }

------------------------------------------------------------------------------
-- The consistent algebra preserves strict increasingness and is always
-- guaranteed to be finite (as the set of simple paths in the network is
-- finite).

isIncreasingá¶œ : IsIncreasing algebra â†’ IsIncreasing algebraá¶œ
isIncreasingá¶œ incr (valid (k , l) , _) (r , _) = incr (A k l) r
isIncreasingá¶œ incr (nothing       , _) (r , _) = âŠ•-identityË¡ r

isStrictlyIncreasingá¶œ : IsStrictlyIncreasing algebra â†’ IsStrictlyIncreasing algebraá¶œ
isStrictlyIncreasingá¶œ sIncr (valid (k , l) , _)     = sIncr (A k l)
isStrictlyIncreasingá¶œ sIncr (nothing       , _) râ‰‰âˆ = (âŠ•-identityË¡ _) , râ‰‰âˆ

isFiniteá¶œ : IsFinite algebraá¶œ
isFiniteá¶œ = allCRoutes , âˆˆ-allCRoutes
  where
  open Membership Sá¶œ using () renaming (_âˆˆ_ to _âˆˆâ‚—_)

  pathToCRoute : Path n â†’ CRoute
  pathToCRoute p = weight A p , weightá¶œ p

  abstract

    allCRoutes : List CRoute
    allCRoutes = map pathToCRoute (allPaths n)

    âˆˆ-allCRoutes : âˆ€ r â†’ r âˆˆâ‚— allCRoutes
    âˆˆ-allCRoutes (r , rá¶œ) = âˆˆ-resp-â‰ˆ Sá¶œ {x = pathToCRoute (path r)} {r , rá¶œ}
      rá¶œ (âˆˆ-mapâº (â„™â‚› n) Sá¶œ weight-cong (âˆˆ-allPaths (path r)))

------------------------------------------------------------------------------
-- Finally the corresponding adjacency matrix for the consitent algebra may be
-- built

Aá¶œ : AdjacencyMatrix algebraá¶œ n
Aá¶œ i j = just (i , j) , toSum (i Fin.â‰Ÿ j)
