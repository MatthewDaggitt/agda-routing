--------------------------------------------------------------------------
-- A proof that F being an ACO implies that the iteration converges over
-- the initial box. The same result is also derived in
-- `RoutingLib.Iteration.Asynchronous.Static.ToDynamic` by going via
-- dynamic iterations. This version of the proof is included for the
-- JAR 2019 paper submission.
--
-- It's also instructive to compare this with the dynamic proof in
-- `RoutingLib.Iteration.Asynchronous.Dynamic.Convergence.ACOImpliesConvergent`
-- in order to appreciate how the addition of epochs and participants
-- complicate the proofs.
--------------------------------------------------------------------------

open import Data.Fin using (Fin)
open import Data.Fin.Subset using (Subset; âŠ¤) renaming (_âˆˆ_ to _âˆˆâ‚›_; _âˆ‰_ to _âˆ‰â‚›_)
open import Data.Fin.Dec using (_âˆˆ?_)
open import Data.Maybe using (just; nothing)
open import Data.Nat renaming (_â‰Ÿ_ to _â‰Ÿâ„•_) hiding (_âŠ”_)
open import Data.Nat.Properties hiding (_â‰Ÿ_)
open import Data.Product as Prod using (âˆƒ; projâ‚‚; projâ‚; _,_; _Ã—_; map)
open import Function using (id; _âˆ˜_; _$_)
open import Induction.WellFounded using (Acc; acc)
open import Induction.Nat using (<-wellFounded)
open import Level using (_âŠ”_)
open import Relation.Binary using (tri<; triâ‰ˆ; tri>)
open import Relation.Binary.PropositionalEquality
  using (_â‰¡_; subst; substâ‚‚; cong; congâ‚‚; refl; sym; trans)
open import Relation.Nullary using (yes; no; Â¬_)
open import Relation.Nullary.Negation using (contradiction)
open import Relation.Unary using (Pred; _âŠ†_; _âˆˆ_)

open import RoutingLib.Relation.Binary.Indexed.Homogeneous
open import RoutingLib.Relation.Unary.Indexed
open import RoutingLib.Relation.Unary.Properties
open import RoutingLib.Function
open import RoutingLib.Function.Reasoning

open import RoutingLib.Iteration.Asynchronous.Static
open import RoutingLib.Iteration.Asynchronous.Static.Convergence.Conditions
open import RoutingLib.Iteration.Asynchronous.Static.Schedule
import RoutingLib.Iteration.Asynchronous.Static.Schedule.Pseudoperiod
  as Pseudoperiods


module RoutingLib.Iteration.Asynchronous.Static.Convergence.ACOImpliesConvergesOver
  {a â„“ â„“â‚ƒ n}
  (Iâˆ¥ : AsyncIterable a â„“ n)
  (aco : ACO Iâˆ¥ â„“â‚ƒ)
   where

open AsyncIterable Iâˆ¥
open ACO  aco


k* : â„•
k* = projâ‚ (B-finish)

x* : S
x* = projâ‚ (projâ‚‚ B-finish)

x*âˆˆB[k*] : x* âˆˆáµ¢ B k* 
x*âˆˆB[k*] = projâ‚ (projâ‚‚ (projâ‚‚ B-finish) â‰¤-refl)

xâˆˆB[k*]â‡’xâ‰ˆx* : âˆ€ {x} â†’ x âˆˆáµ¢ B k* â†’ x â‰ˆ x* 
xâˆˆB[k*]â‡’xâ‰ˆx* = projâ‚‚ (projâ‚‚ (projâ‚‚ B-finish) â‰¤-refl)

x*-fixed : F x* â‰ˆ x*
x*-fixed = beginâŸ¨ x*âˆˆB[k*] âŸ©
  â‡’ x*   âˆˆáµ¢ B k*       âˆ´âŸ¨ F-mono-B âŸ©
  â‡’ F x* âˆˆáµ¢ B (suc k*) âˆ´âŸ¨ projâ‚‚ (projâ‚‚ (projâ‚‚ B-finish) (nâ‰¤1+n k*)) âŸ©
  â‡’ F x* â‰ˆ x*          âˆ

------------------------------------------------------------------------
-- Notation

module _ {xâ‚€ : S} (xâ‚€âˆˆBâ‚€ : xâ‚€ âˆˆáµ¢ B 0) (ğ“¢ : Schedule n) where

  open Schedule ğ“¢
  open Pseudoperiods ğ“¢

  -- Some shorthand notation where the epoch and participant indices are
  -- replaced with a time index.

  Î´' : S â†’ âˆ€ {t} â†’ Acc _<_ t â†’ S
  Î´' = asyncIter' Iâˆ¥ ğ“¢

  Î´ : S â†’ ğ•‹ â†’ S
  Î´ xâ‚€ t = Î´' xâ‚€ (<-wellFounded t)


  -- The concept of being locally safe

  StateOfNode_InBox_AtTime_ : Fin n â†’ â„• â†’ ğ•‹ â†’ Set â„“â‚ƒ
  StateOfNode i InBox k AtTime t = (acc : Acc _<_ t) â†’ Î´' xâ‚€ acc i âˆˆ B k i

  StateInBox_AtTime_ : â„• â†’ ğ•‹ â†’ Set â„“â‚ƒ
  StateInBox k AtTime t = âˆ€ i â†’ StateOfNode i InBox k AtTime t

  MessagesOfNode_InBox_AtTime_ : Fin n â†’ â„• â†’ ğ•‹ â†’ Set â„“â‚ƒ
  MessagesOfNode i InBox k AtTime t = âˆ€ {j s} â†’ t < s â†’ (acc : Acc _<_ (Î² s i j)) â†’ Î´' xâ‚€ acc j âˆˆ B k j

  MessagesInBox_AtTime_ : â„• â†’ ğ•‹ â†’ Set â„“â‚ƒ
  MessagesInBox k AtTime t = âˆ€ i â†’ MessagesOfNode i InBox k AtTime t

  ComputationInBox_AtTime_ : â„• â†’ ğ•‹ â†’ Set â„“â‚ƒ
  ComputationInBox k AtTime t = MessagesInBox (k âˆ¸ 1) AtTime t Ã— StateInBox k AtTime t

--------------------------------------------------------------------------
-- Actual proofs
--------------------------------------------------------------------------
-- Base case: the asynchronous iteration is always in the initial box

  stateâˆˆBâ‚€ : âˆ€ t â†’ StateInBox 0 AtTime t
  stateâˆˆBâ‚€ zero    i (acc rec) = xâ‚€âˆˆBâ‚€ i
  stateâˆˆBâ‚€ (suc t) i (acc rec) with i âˆˆ? Î± (suc t)
  ... | no  _ = stateâˆˆBâ‚€ t i (rec t _)
  ... | yes _ = F-resp-Bâ‚€ (Î» j â†’ stateâˆˆBâ‚€ (Î² (suc t) i j) j _) i 

  messagesâˆˆBâ‚€ : âˆ€ t â†’ MessagesInBox 0 AtTime t
  messagesâˆˆBâ‚€ t i {j} {s} t<s rec = stateâˆˆBâ‚€ (Î² s i j) j rec

  computationâˆˆBâ‚€ : âˆ€ t â†’ ComputationInBox 0 AtTime t
  computationâˆˆBâ‚€ t = messagesâˆˆBâ‚€ t , stateâˆˆBâ‚€ t
  
--------------------------------------------------------------------------
-- Preservation: if the asynchronous iteration is in a box, 
-- then it will still be in that box in the future.

  state-steps : âˆ€ {k s e} â†’ s â‰¤ e â†’
                ComputationInBox k AtTime s â†’
                StateInBox k AtTime e
  state-steps {k}     {s} {zero}  zâ‰¤n   câˆˆBâ‚– = projâ‚‚ câˆˆBâ‚–
  state-steps {zero}  {s} {suc e} sâ‰¤1+e câˆˆBâ‚– i rec = stateâˆˆBâ‚€ (suc e) i rec
  state-steps {suc k} {s} {suc e} sâ‰¤1+e câˆˆBâ‚– i (acc rec) with <-cmp s (suc e)
  ... | triâ‰ˆ _ refl _      = projâ‚‚ câˆˆBâ‚– i (acc rec)
  ... | tri> _ _ s>1+e     = contradiction sâ‰¤1+e (<â‡’â‰± s>1+e)
  ... | tri< (sâ‰¤s sâ‰¤e) _ _ with i âˆˆ? Î± (suc e)
  ...   | no  _ = state-steps sâ‰¤e câˆˆBâ‚– i (rec e â‰¤-refl)
  ...   | yes _ = F-mono-B (Î» j â†’ projâ‚ câˆˆBâ‚– i (sâ‰¤s sâ‰¤e) _) i

  message-steps : âˆ€ {k s e} â†’
                  s â‰¤ e â†’
                  MessagesInBox k AtTime s â†’
                  MessagesInBox k AtTime e
  message-steps sâ‰¤e mâˆˆb i e<t recÎ² = mâˆˆb i (<-transÊ³ sâ‰¤e e<t) recÎ²

--------------------------------------------------------------------------
-- Step: after one pseudoperiod the node is guaranteed to have
-- advanced at least one box

  advance-stateáµ¢ : âˆ€ {s e i k} â†’
                   i IsActiveIn [ s , e ] â†’
                   MessagesOfNode i InBox k AtTime s â†’
                   StateOfNode i InBox (suc k) AtTime e
  advance-stateáµ¢ {s} {zero}  {i} (mkâ‚áµ¢ m ()  zâ‰¤n   iâˆˆÎ±â‚˜)
  advance-stateáµ¢ {s} {suc e} {i} (mkâ‚áµ¢ m s<m mâ‰¤1+e iâˆˆÎ±â‚˜) mâˆˆBâ‚– (acc recâ‚‘)
    with i âˆˆ? Î± (suc e)
  ...   | yes _ = F-mono-B (Î» j â†’ mâˆˆBâ‚– (â‰¤-trans s<m mâ‰¤1+e) _) i
  ...   | no  iâˆ‰Î±â‚â‚Šâ‚‘ with m â‰Ÿâ„• suc e
  ...     | yes refl  = contradiction iâˆˆÎ±â‚˜ iâˆ‰Î±â‚â‚Šâ‚‘
  ...     | no  mâ‰¢1+e = advance-stateáµ¢ (mkâ‚áµ¢ m s<m mâ‰¤e iâˆˆÎ±â‚˜) mâˆˆBâ‚– _
    where mâ‰¤e = â‰¤-pred (â‰¤âˆ§â‰¢â‡’< mâ‰¤1+e mâ‰¢1+e)

  advance-state : âˆ€ {s e k} â†’
                  ActivationPeriod [ s , e ] â†’
                  MessagesInBox k AtTime s â†’
                  StateInBox (suc k) AtTime e
  advance-state {s} {e} {k} (mkâ‚ v activeáµ¢) mâˆˆBâ‚– i
    = advance-stateáµ¢ (activeáµ¢ i) (mâˆˆBâ‚– i)

  advance-messages : âˆ€ {s e k} â†’
                     ExpiryPeriod [ s , e ] â†’
                     ComputationInBox k AtTime s â†’
                     MessagesInBox k AtTime e
  advance-messages {s} (mkâ‚‘ _ expiryáµ¢) câˆˆBâ‚– i {j} e<t recÎ²
    = state-steps (expiryáµ¢ i j e<t) câˆˆBâ‚– j recÎ²

--------------------------------------------------------------------------
-- Steps : after k pseudoperiods all nodes are guaranteed to have
-- advanced at least k boxes

  messages-pp : âˆ€ {s e k} â†’
                Pseudoperiod [ s , e ] â†’
                ComputationInBox k       AtTime s â†’
                ComputationInBox (suc k) AtTime e
  messages-pp pp câˆˆBâ‚– = mâˆˆBâ‚–áµ‰ , sâˆˆBâ‚â‚Šâ‚–
    where
    open Pseudoperiod pp
    mâˆˆBâ‚–áµ  = advance-messages Î²[s,m] câˆˆBâ‚–
    mâˆˆBâ‚–áµ‰   = message-steps midâ‰¤end mâˆˆBâ‚–áµ
    sâˆˆBâ‚â‚Šâ‚–  = advance-state Î±[m,e] mâˆˆBâ‚–áµ
  
  messages-mpp : âˆ€ {s e k n} â†’
                 MultiPseudoperiod n [ s , e ] â†’
                 ComputationInBox k       AtTime s â†’
                 ComputationInBox (n + k) AtTime e
  messages-mpp {_} {_} {_} {zero}  none            câˆˆBâ‚– = câˆˆBâ‚–
  messages-mpp {s} {e} {k} {suc n} (next m pp mpp) câˆˆBâ‚– = beginâŸ¨ câˆˆBâ‚– âŸ©
    â‡’ ComputationInBox k           AtTime s âˆ´âŸ¨ messages-pp pp âŸ©
    â‡’ ComputationInBox (suc k)     AtTime m âˆ´âŸ¨ messages-mpp mpp âŸ©
    â‡’ ComputationInBox (n + suc k) AtTime e âˆ´âŸ¨ subst (ComputationInBox_AtTime e) (+-suc n k) âŸ©
    â‡’ ComputationInBox (suc n + k) AtTime e âˆ

--------------------------------------------------------------------------
-- Convergence

  computationâˆˆBâ‚– : âˆ€ {s e k} â†’
                   MultiPseudoperiod k [ s , e ] â†’
                   ComputationInBox k AtTime e
  computationâˆˆBâ‚– {s} {e} {zero}  none = computationâˆˆBâ‚€ s
  computationâˆˆBâ‚– {s} {e} {suc k} (next m pp mpp) = beginâŸ¨ computationâˆˆBâ‚€ s âŸ©
    â‡’ ComputationInBox 0       AtTime s âˆ´âŸ¨ messages-pp pp âŸ©
    â‡’ ComputationInBox 1       AtTime m âˆ´âŸ¨ messages-mpp mpp âŸ©
    â‡’ ComputationInBox (k + 1) AtTime e âˆ´âŸ¨ subst (ComputationInBox_AtTime e) (+-comm k 1) âŸ©
    â‡’ ComputationInBox (1 + k) AtTime e âˆ

  x*-reached : âˆ€ {s m e : ğ•‹} â†’
               MultiPseudoperiod k* [ s , m ] â†’
               m â‰¤ e â†’ 
               Î´ xâ‚€ e â‰ˆ x*
  x*-reached {s} {m} {e} mpp mâ‰¤e = beginâŸ¨ mpp âŸ©
    â‡’ MultiPseudoperiod k* [ s , m ]  âˆ´âŸ¨ computationâˆˆBâ‚– âŸ©
    â‡’ ComputationInBox k* AtTime m    âˆ´âŸ¨ state-steps mâ‰¤e âŸ©
    â‡’ StateInBox k* AtTime e          âˆ´âŸ¨ (Î» prf i â†’ prf i (<-wellFounded e)) âŸ©
    â‡’ Î´ xâ‚€ e âˆˆáµ¢ B k*                  âˆ´âŸ¨ xâˆˆB[k*]â‡’xâ‰ˆx* âŸ©
    â‡’ Î´ xâ‚€ e â‰ˆ x*                     âˆ

convergent : ConvergesOver Iâˆ¥ (B 0) 
convergent = record
  { x*         = x*
  ; k*         = k*
  ; x*-fixed   = x*-fixed
  ; x*-reached = x*-reached
  }
