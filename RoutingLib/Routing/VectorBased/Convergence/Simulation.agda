--------------------------------------------------------------------------------
-- Agda routing library
--
-- The proof that if algebra A simulates algebra B then if the vector-based
-- protocol based on A converges then so does the vector-based protocol based
-- on algebra B. This is based on a reduction to the simulation results for
-- general dynamic asynchronous iterations found in `RoutingLib.Iteration`.
--------------------------------------------------------------------------------

open import Data.Fin using (Fin; _â‰Ÿ_)
open import Data.Fin.Subset using (Subset)
open import Data.Fin.Subset.Properties using (_âˆˆ?_)
open import Data.List using (foldr; tabulate; map)
open import Data.List.Properties using (tabulate-cong; map-tabulate)
open import Data.List.Relation.Unary.All using (All; []; _âˆ·_)
open import Data.List.Relation.Unary.All.Properties using (tabulateâº)
open import Data.List.Relation.Unary.AllPairs using (AllPairs; []; _âˆ·_)
open import Data.List.Relation.Binary.Equality.Setoid using (foldrâº)
import Data.List.Relation.Unary.AllPairs.Properties as AllPairs
open import Data.Nat using (â„•; _+_)
open import Function using (_âˆ˜_; _âˆ˜â‚‚â€²_)
open import Level using (Level; _âŠ”_) renaming (suc to lsuc)
open import Relation.Binary using (REL)
open import Relation.Nullary using (yes; no)
open import Relation.Binary.PropositionalEquality using (_â‰¡_; cong)
import Relation.Binary.Reasoning.Setoid as SetoidReasoning

open import RoutingLib.Routing.Prelude using (Network; RoutingMatrix; AdjacencyMatrix; I)
open import RoutingLib.Data.Matrix using (SquareMatrix)
open import RoutingLib.Data.List.Properties using (foldr-map-commute-genâ‚‚)

open import RoutingLib.Routing.VectorBased.Convergence.Definitions
open import RoutingLib.Iteration.Asynchronous.Dynamic as Async using (Epoch)
import RoutingLib.Iteration.Asynchronous.Dynamic.Convergence as Async
open import RoutingLib.Routing.Algebra
open import RoutingLib.Routing.Algebra.Simulation
import RoutingLib.Routing.Algebra.Comparable as Comparable
import RoutingLib.Routing.VectorBased.Asynchronous as AsyncBellmanFord
import RoutingLib.Routing.VectorBased.Synchronous as SyncBellmanFord

module RoutingLib.Routing.VectorBased.Convergence.Simulation
  {aâ‚ bâ‚ â„“â‚ aâ‚‚ bâ‚‚ â„“â‚‚}
  {A : RawRoutingAlgebra aâ‚ bâ‚ â„“â‚}
  {B : RawRoutingAlgebra aâ‚‚ bâ‚‚ â„“â‚‚}
  (Aâ‡‰B : A Simulates B)
  where

open RawRoutingAlgebra hiding (_â‰Ÿ_)
open RawRoutingAlgebra A using ()
  renaming (_â‰ˆ_ to _â‰ˆáµƒ_; _âŠ•_ to _âŠ•áµƒ_; _â–·_ to _â–·áµƒ_; 0# to 0#áµƒ; âˆ# to âˆáµƒ)
open RawRoutingAlgebra B using ()
  renaming (_â‰ˆ_ to _â‰ˆáµ‡_; _âŠ•_ to _âŠ•áµ‡_; _â–·_ to _â–·áµ‡_; 0# to 0#áµ‡; âˆ# to âˆáµ‡; â‰ˆ-refl to â‰ˆáµ‡-refl)
open _Simulates_ Aâ‡‰B

open import Data.List.Relation.Binary.Equality.Setoid (S B) as â‰‹ using (â‰‹-reflexive)

private
  variable
    n : â„•
    e : Epoch
    p : Subset n
    i : Fin n

--------------------------------------------------------------------------------
-- Proof

from-injective : âˆ€ {x y} â†’ from x â‰ˆáµƒ from y â†’ x â‰ˆáµ‡ y
from-injective {x} {y} f[x]â‰ˆf[y] = begin
  x           â‰ˆË˜âŸ¨ to-from x âŸ©
  to (from x) â‰ˆâŸ¨  to-cong f[x]â‰ˆf[y] âŸ©
  to (from y) â‰ˆâŸ¨  to-from y âŸ©
  y           âˆ
  where open SetoidReasoning (S B)

toâ‚˜ : RoutingMatrix A n â†’ RoutingMatrix B n
toâ‚˜ X i j = to (X i j)

fromâ‚˜ : RoutingMatrix B n â†’ RoutingMatrix A n
fromâ‚˜ X i j = from (X i j)

toâ‚ : AdjacencyMatrix A n â†’ AdjacencyMatrix B n
toâ‚ N i j = toâ‚› (N i j)

fromâ‚ : AdjacencyMatrix B n â†’ AdjacencyMatrix A n
fromâ‚ N i j = fromâ‚› (N i j)

module _ {n : â„•} where

  open RoutingLib.Routing.Prelude A n using () renaming (I to Iáµƒ)
  open RoutingLib.Routing.Prelude B n using () renaming (I to Iáµ‡)

  toIáµƒâ‰ˆIáµ‡ : âˆ€ i j â†’ to (Iáµƒ i j) â‰ˆáµ‡ Iáµ‡ i j
  toIáµƒâ‰ˆIáµ‡ i j with j â‰Ÿ i
  ... | yes _ = to-0#
  ... | no  _ = to-âˆ

module _ (Aáµƒ : AdjacencyMatrix A n) (Aáµ‡ : AdjacencyMatrix B n)
         (Aáµƒâ‰¡Aáµ‡ : âˆ€ i j â†’ toâ‚› (Aáµƒ i j) â‰¡ Aáµ‡ i j)
         where
  
  open SyncBellmanFord A Aáµƒ using () renaming (Ïƒ to Ïƒá´¬; F to Fáµƒ; I to Iáµƒ)
  open SyncBellmanFord B Aáµ‡ using () renaming (Ïƒ to Ïƒá´®; F to Fáµ‡; I to Iáµ‡; F-cong to Fáµ‡-cong)
  open Comparable A
  
  All-â‰-tabulate : âˆ€ (X : RoutingMatrix A n) j â†’ All (_â‰ Iáµƒ i j) (tabulate (Î» k â†’ Aáµƒ i k â–·áµƒ X k j))
  All-â‰-tabulate {i} X j with j â‰Ÿ i
  ... | yes _ = tabulateâº (Î» k â†’ e0# (Aáµƒ i k) (X k j) (â‰ˆ-refl A) (â‰ˆ-refl A))
  ... | no  _ = tabulateâº (Î» k â†’ eâˆ# (Aáµƒ i k) (X k j) (â‰ˆ-refl A) (â‰ˆ-refl A))
  
  AllPairs-â‰-tabulate : âˆ€ (X : RoutingMatrix A n) j â†’ AllPairs _â‰_ (tabulate (Î» k â†’ Aáµƒ i k â–·áµƒ X k j))
  AllPairs-â‰-tabulate {i} X j = AllPairs.tabulateâº (Î» iâ‰¢j â†’ ee# (Aáµƒ i _) (Aáµƒ i _) (X _ j) (X _ j) iâ‰¢j (â‰ˆ-refl A) (â‰ˆ-refl A))
  
  F-simulate : âˆ€ (X : RoutingMatrix A n) j â†’ to (Fáµƒ X i j) â‰ˆáµ‡ Fáµ‡ (toâ‚˜ X) i j
  F-simulate {i} X j = begin
      to (Fáµƒ X i j)
    â‰¡âŸ¨âŸ©
      to (foldr _âŠ•áµƒ_ (Iáµƒ i j) (tabulate (Î» k â†’ Aáµƒ i k â–·áµƒ X k j)))
    â‰ˆâŸ¨ â‰ˆ-sym B (foldr-map-commute-genâ‚‚ (S B) (âŠ•-cong B) âŠ•-pres-â‰ to-âŠ• (All-â‰-tabulate X j) (AllPairs-â‰-tabulate X j)) âŸ©
      foldr _âŠ•áµ‡_ (to (Iáµƒ i j)) (map to (tabulate Î» k â†’ Aáµƒ i k â–·áµƒ X k j))
    â‰ˆâŸ¨ foldrâº (S B) (âŠ•-cong B) (toIáµƒâ‰ˆIáµ‡ i j) (â‰‹-reflexive (map-tabulate (Î» k â†’ Aáµƒ i k â–·áµƒ X k j) to)) âŸ©
      foldr _âŠ•áµ‡_ (Iáµ‡ i j) (tabulate (Î» k â†’ to (Aáµƒ i k â–·áµƒ X k j)))
    â‰ˆâŸ¨ foldrâº (S B) (âŠ•-cong B) (â‰ˆ-refl B) (â‰‹.tabulateâº (Î» k â†’ to-â–· (Aáµƒ i k) (X k j)) ) âŸ©
      foldr _âŠ•áµ‡_ (Iáµ‡ i j) (tabulate (Î» k â†’ toâ‚› (Aáµƒ i k) â–·áµ‡ to (X k j)))
    â‰¡âŸ¨ cong (foldr _âŠ•áµ‡_ (Iáµ‡ i j)) (tabulate-cong {n = n} Î» k â†’ cong (_â–·áµ‡ _) (Aáµƒâ‰¡Aáµ‡ i k)) âŸ©
      foldr _âŠ•áµ‡_ (Iáµ‡ i j) (tabulate (Î» k â†’ Aáµ‡ i k â–·áµ‡ to (X k j)))
    â‰¡âŸ¨âŸ©
      Fáµ‡ (Î» k l â†’ to (X k l)) i j
    âˆ where open SetoidReasoning (S B)

  Ïƒ-simulate : âˆ€ t X i j â†’ to (Ïƒá´¬ t X i j) â‰ˆáµ‡ Ïƒá´® t (toâ‚˜ X) i j
  Ïƒ-simulate â„•.zero    X i j = â‰ˆáµ‡-refl
  Ïƒ-simulate (â„•.suc t) X i j = begin
    to (Ïƒá´¬ (â„•.suc t) X i j)  â‰¡âŸ¨âŸ©
    to (Fáµƒ (Ïƒá´¬ t X) i j)     â‰ˆâŸ¨ F-simulate (Ïƒá´¬ t X) j âŸ©
    Fáµ‡ (toâ‚˜ (Ïƒá´¬ t X)) i j    â‰ˆâŸ¨ Fáµ‡-cong (Ïƒ-simulate t X) i j âŸ©
    Fáµ‡ (Ïƒá´® t (toâ‚˜ X)) i j    â‰¡âŸ¨âŸ©
    Ïƒá´® (â„•.suc t) (toâ‚˜ X) i j âˆ
    where open SetoidReasoning (S B)
    
module _ (Náµ‡ : Network B n) where

  Náµƒ : Network A n
  Náµƒ e = fromâ‚ (Náµ‡ e)

  open AsyncBellmanFord A Náµƒ using (RoutingMatrix) renaming (Fâ€² to Fáµƒ; Fâˆ¥ to Fâˆ¥áµƒ; I to Iáµƒ; Aâ‚œ to Aáµƒ)
  open AsyncBellmanFord B Náµ‡ using () renaming (Fâ€² to Fáµ‡; Fâˆ¥ to Fâˆ¥áµ‡; I to Iáµ‡; Aâ‚œ to Aáµ‡; F-cong to Fáµ‡-cong)

  toA : âˆ€ i j â†’ toâ‚› (Aáµƒ e p i j) â‰¡ Aáµ‡ e p i j
  toA {e} {p} i j with i âˆˆ? p | j âˆˆ? p
  ... | no  _ | _     = to-fâˆ
  ... | yes _ | no  _ = to-fâˆ
  ... | yes _ | yes _ = toâ‚›-fromâ‚› (Náµ‡ e i j)

  to-Fâ€² : âˆ€ {e p} X j â†’ to (Fáµƒ e p X i j) â‰ˆáµ‡ Fáµ‡ e p (Î» k l â†’ to (X k l)) i j
  to-Fâ€² {e = e} {p} = F-simulate (Aáµƒ e p) (Aáµ‡ e p) toA 
  
  Fâˆ¥áµƒâ‡‰Fâˆ¥áµ‡ : Fâˆ¥áµƒ Async.Simulates Fâˆ¥áµ‡
  Fâˆ¥áµƒâ‡‰Fâˆ¥áµ‡ = record
    { toáµ¢       = to âˆ˜_
    ; fromáµ¢     = from âˆ˜_
    ; toáµ¢-âŠ¥     = Î» {i} â†’ toIáµƒâ‰ˆIáµ‡ i
    ; toáµ¢-cong  = to-cong âˆ˜_
    ; toáµ¢-F     = to-Fâ€²
    ; toáµ¢-fromáµ¢ = to-from âˆ˜_
    }

open AsyncBellmanFord using (Fâˆ¥)
open SyncBellmanFord using (Ïƒ; Ïƒ-cong; â„ğ•„â‚›)

convergent : Convergent A â†’ Convergent B
convergent convergent Náµ‡ = Async.simulate (Fâˆ¥áµƒâ‡‰Fâˆ¥áµ‡ Náµ‡) (convergent (fromâ‚ âˆ˜ Náµ‡))

syncConvergesIn : âˆ€ f â†’ SynchronouslyConvergesIn A f â†’ SynchronouslyConvergesIn B f 
syncConvergesIn f convergesIn {n} Aá´® X t = begin
  Ïƒ B Aá´® (f n + t)                     X   â‰ˆË˜âŸ¨ Ïƒ-cong B Aá´® (f n + t) (Î» i j â†’ to-from (X i j)) âŸ©
  Ïƒ B Aá´® (f n + t)         (toâ‚˜ (fromâ‚˜ X)) â‰ˆË˜âŸ¨ Ïƒ-simulate (fromâ‚ Aá´®) Aá´® (Î» i j â†’ toâ‚›-fromâ‚› (Aá´® i j)) (f n + t) (fromâ‚˜ X) âŸ© 
  toâ‚˜ (Ïƒ A (fromâ‚ Aá´®) (f n + t) (fromâ‚˜ X)) â‰ˆâŸ¨ (Î» i j â†’ to-cong (convergesIn (fromâ‚ Aá´®) (fromâ‚˜ X) t i j)) âŸ©
  toâ‚˜ (Ïƒ A (fromâ‚ Aá´®) (f n)     (fromâ‚˜ X)) â‰ˆâŸ¨ Ïƒ-simulate (fromâ‚ Aá´®) Aá´® (Î» i j â†’ toâ‚›-fromâ‚› (Aá´® i j)) (f n) (fromâ‚˜ X) âŸ©
  Ïƒ B Aá´® (f n)             (toâ‚˜ (fromâ‚˜ X)) â‰ˆâŸ¨ Ïƒ-cong B Aá´® (f n) (Î» i j â†’ to-from (X i j)) âŸ©
  Ïƒ B Aá´® (f n)                         X   âˆ
  where open SetoidReasoning (â„ğ•„â‚› B Aá´®)
